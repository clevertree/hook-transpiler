plugins {
    id 'com.android.library'
    id 'org.jetbrains.kotlin.android'
    id 'maven-publish'
    id 'signing'
}

android {
    namespace = "com.clevertree.hooktranspiler"
    compileSdk = 33
    
    buildFeatures {
        buildConfig = true
    }

    defaultConfig {
        minSdk = 24
        targetSdk = 33
        testInstrumentationRunner = "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
        
        buildConfigField "String", "VERSION_NAME", "\"1.3.23\""
        buildConfigField "String", "JSCBRIDGE_VERSION", "\"1.0.0\""
        buildConfigField "String", "THEMED_STYLER_VERSION", "\"1.2.9\""
    }

    buildTypes {
        release {
            minifyEnabled = false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    lint {
        abortOnError = false
    }

    compileOptions {
        sourceCompatibility = JavaVersion.VERSION_11
        targetCompatibility = JavaVersion.VERSION_11
    }

    kotlinOptions {
        jvmTarget = '11'
    }

    sourceSets {
        main {
            jniLibs.srcDirs = ['jniLibs']
        }
    }

    publishing {
        singleVariant("release") {
            withSourcesJar()
            withJavadocJar()
        }
    }
}

dependencies {
    // Core Android
    implementation 'androidx.core:core-ktx:1.10.1'
    implementation 'androidx.appcompat:appcompat:1.6.1'

    // Kotlin
    implementation 'org.jetbrains.kotlin:kotlin-stdlib:1.9.0'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.7.1'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.7.1'

    // JSON parsing
    implementation 'com.google.code.gson:gson:2.10.1'

    // JSC Bridge
    implementation 'com.clevertree:jscbridge:1.0.0'

    // Themed Styler
    implementation 'com.clevertree:themed-styler-android:1.2.9'

    // Markdown Parser
    implementation 'com.clevertree:md2jsx-android:1.0.2'

    // OkHttp for HTTP requests
    implementation 'com.squareup.okhttp3:okhttp:4.11.0'

    // Testing
    testImplementation 'junit:junit:4.13.2'
    testImplementation 'org.jetbrains.kotlinx:kotlinx-coroutines-test:1.7.1'
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'
}

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                from components.release

                groupId = 'com.clevertree'
                artifactId = 'hook-transpiler-android'
                version = '1.3.28'

                pom {
                    name = 'Hook Transpiler for Android'
                    description = 'Native Kotlin implementation of JSX hook transpiler and renderer for Android'
                    url = 'https://github.com/clevertree/hook-transpiler'

                    licenses {
                        license {
                            name = 'MIT OR Apache-2.0'
                            url = 'https://opensource.org/licenses/MIT'
                        }
                    }

                    developers {
                        developer {
                            id = 'clevertree'
                            name = 'Ari Asulin'
                            email = 'ari.asulin@gmail.com'
                        }
                    }

                    scm {
                        connection = 'scm:git:github.com/clevertree/hook-transpiler.git'
                        developerConnection = 'scm:git:ssh://github.com/clevertree/hook-transpiler.git'
                        url = 'https://github.com/clevertree/hook-transpiler'
                    }
                }
            }
        }

        repositories {
            maven {
                name = "OSSRH"
                url = "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"
                credentials {
                    username = project.findProperty("ossrhUsername") ?: System.getenv("OSSRH_USERNAME")
                    password = project.findProperty("ossrhPassword") ?: System.getenv("OSSRH_PASSWORD")
                }
            }
            mavenLocal()
        }
    }

    signing {
        required { gradle.taskGraph.hasTask("publish") }
        def signingKey = project.findProperty("signing.secretKey")
        def signingPassword = project.findProperty("signing.password")
        if (signingKey) {
            useInMemoryPgpKeys(signingKey, signingPassword ?: "")
        }
        sign publishing.publications.release
    }
}
