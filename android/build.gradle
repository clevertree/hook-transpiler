plugins {
    id 'com.android.library'
    id 'org.jetbrains.kotlin.android'
    id 'maven-publish'
}

android {
    namespace = "com.clevertree.hooktranspiler"
    compileSdk = 33

    defaultConfig {
        minSdk = 24
        targetSdk = 33
        testInstrumentationRunner = "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
    }

    buildTypes {
        release {
            minifyEnabled = false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    lint {
        abortOnError = false
    }

    compileOptions {
        sourceCompatibility = JavaVersion.VERSION_11
        targetCompatibility = JavaVersion.VERSION_11
    }

    kotlinOptions {
        jvmTarget = '11'
    }

    sourceSets {
        main {
            jniLibs.srcDirs = ['jniLibs']
        }
    }

    publishing {
        singleVariant("release") {
            withSourcesJar()
        }
    }
}

dependencies {
    // Core Android
    implementation 'androidx.core:core-ktx:1.10.1'
    implementation 'androidx.appcompat:appcompat:1.6.1'

    // Kotlin
    implementation 'org.jetbrains.kotlin:kotlin-stdlib:1.9.0'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.7.1'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.7.1'

    // JSON parsing
    implementation 'com.google.code.gson:gson:2.10.1'

    // JavaScriptCore
    implementation 'org.webkit:android-jsc:+'

    // OkHttp for HTTP requests
    implementation 'com.squareup.okhttp3:okhttp:4.11.0'

    // Testing
    testImplementation 'junit:junit:4.13.2'
    testImplementation 'org.jetbrains.kotlinx:kotlinx-coroutines-test:1.7.1'
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'
}

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                from components.release

                groupId = 'com.clevertree'
                artifactId = 'hook-transpiler-android'
                version = '1.3.9'

                pom {
                    name = 'Hook Transpiler for Android'
                    description = 'Native Kotlin implementation of JSX hook transpiler and renderer for Android'
                    url = 'https://github.com/clevertree/hook-transpiler'

                    licenses {
                        license {
                            name = 'MIT OR Apache-2.0'
                        }
                    }

                    developers {
                        developer {
                            id = 'clevertree'
                            name = 'Ari Asulin'
                        }
                    }

                    scm {
                        connection = 'scm:git:github.com/clevertree/hook-transpiler.git'
                        url = 'https://github.com/clevertree/hook-transpiler'
                    }
                }
            }
        }

        repositories {
            mavenLocal()
        }
    }
}
